---
subcategory: "IoT Hub"
layout: "azurerm"
page_title: "Azure Resource Manager: azurerm_iothub_file_upload"
description: |-
  Manages the File Upload of an IoT Hub.
---

# azurerm_iothub_file_upload

Manages the File Upload of an IoT Hub.

~> **Note:** File upload can be defined either directly on the `azurerm_iothub` resource, or using the `azurerm_iothub_file_upload` resource - but the two cannot be used together. If both are used against the same IoTHub, spurious changes will occur.

## Example Usage

```hcl
resource "azurerm_resource_group" "example" {
  name     = "example-resources"
  location = "West Europe"
}

resource "azurerm_storage_account" "example" {
  name                     = "examplestorage"
  resource_group_name      = azurerm_resource_group.example.name
  location                 = azurerm_resource_group.example.location
  account_tier             = "Standard"
  account_replication_type = "LRS"
}

resource "azurerm_storage_container" "example" {
  name                  = "examplecontainer"
  storage_account_name  = azurerm_storage_account.example.name
  container_access_type = "private"
}

resource "azurerm_iothub" "example" {
  name                = "example"
  resource_group_name = azurerm_resource_group.example.name
  location            = azurerm_resource_group.example.location

  sku {
    name     = "S1"
    capacity = "1"
  }

  lifecycle {
    ignore_changes = [
      file_upload
    ]
  }
}

resource "azurerm_iothub_file_upload" "example" {
  iothub_id         = azurerm_iothub.example.id
  connection_string = azurerm_storage_account.example.primary_blob_connection_string
  container_name    = azurerm_storage_container.example.name
}
```

## Arguments Reference

The following arguments are supported:

* `iothub_id` - (Required) The ID of the IoT Hub. Changing this forces a new IoT Hub to be created.

* `connection_string` - (Required) The connection string for the Azure Storage account to which files are uploaded.

* `container_name` - (Required) The name of the root container where the files should be uploaded to. The container need not exist but should be creatable using the `connection_string` specified.

---

* `authentication_type` - (Optional) The type used to authenticate against the storage account. Possible values are `keyBased` and `identityBased`. Defaults to `keyBased`.

* `default_ttl` - (Optional) The period of time for which a file upload notification message is available to consume before it expires, specified as an [ISO 8601 timespan duration](https://en.wikipedia.org/wiki/ISO_8601#Durations). This value must be between 1 minute and 48 hours. Defaults to `PT1H`.

* `identity_id` - (Optional) The ID of the User Managed Identity used to authenticate against the storage account.

-> **Note:** `identity_id` can only be specified when `authentication_type` is `identityBased`. It must be one of the `identity_ids` of the IoT Hub. If `identity_id` is omitted when `authentication_type` is `identityBased`, then the System-Assigned Managed Identity of the IoT Hub will be used.

* `lock_duration` - (Optional) The lock duration for the file upload notifications queue, specified as an [ISO 8601 timespan duration](https://en.wikipedia.org/wiki/ISO_8601#Durations). This value must be between 5 and 300 seconds. Defaults to `PT1M`.

* `max_delivery_count` - (Optional) The number of times the IoT Hub attempts to deliver a file upload notification message. Defaults to `10`.

* `notifications_enabled` - (Optional) Used to specify whether file notifications are sent to IoT Hub on upload. Defaults to `false`.

* `sas_ttl` - (Optional) The period of time for which the SAS URI generated by IoT Hub for file upload is valid, specified as an [ISO 8601 timespan duration](https://en.wikipedia.org/wiki/ISO_8601#Durations). This value must be between 1 minute and 24 hours. Defaults to `PT1H`.

## Attributes Reference

In addition to the Arguments listed above - the following Attributes are exported: 

* `id` - The ID of the IoT Hub.

## Timeouts

The `timeouts` block allows you to specify [timeouts](https://www.terraform.io/language/resources/syntax#operation-timeouts) for certain actions:

* `create` - (Defaults to 30 minutes) Used when creating the IoT Hub.
* `read` - (Defaults to 5 minutes) Used when retrieving the IoT Hub.
* `update` - (Defaults to 30 minutes) Used when updating the IoT Hub.
* `delete` - (Defaults to 30 minutes) Used when deleting the IoT Hub.

## Import

IoT Hub File Uploads can be imported using the `resource id`, e.g.

```shell
terraform import azurerm_iothub_file_upload.example /subscriptions/00000000-0000-0000-0000-000000000000/resourceGroups/mygroup1/providers/Microsoft.Devices/iotHubs/hub1
```
